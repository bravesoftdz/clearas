{ *********************************************************************** }
{                                                                         }
{ Clearas Main Unit                                                       }
{                                                                         }
{ Copyright (c) 2011-2014 P.Meisberger (PM Code Works)                    }
{                                                                         }
{ *********************************************************************** }

unit ClearasMain;

interface

uses
  Windows, SysUtils, Classes, Controls, Forms, ComCtrls, StdCtrls, ExtCtrls,
  Dialogs, Menus, Graphics, ShellAPI, ClearasAPI, ClearasInfo, LanguageFile,
  OSUtils, Updater;

type
  TMain = class(TForm, IChangeLanguageListener, IUpdateListener)
    PopupMenu: TPopupMenu;
    pmDeactivate: TMenuItem;
    N1: TMenuItem;
    pmExport: TMenuItem;
    pmDelete: TMenuItem;
    MainMenu: TMainMenu;
    mmView: TMenuItem;
    mmRefresh: TMenuItem;
    N2: TMenuItem;
    pmInfos: TMenuItem;
    mmHelp: TMenuItem;
    mmInfo: TMenuItem;
    mmEdit: TMenuItem;
    mmContext: TMenuItem;
    mmFile: TMenuItem;
    mmExportList: TMenuItem;
    N3: TMenuItem;
    mmClose: TMenuItem;
    mmExport: TMenuItem;
    mmAdd: TMenuItem;
    N4: TMenuItem;
    N5: TMenuItem;
    mmDate: TMenuItem;
    mmImport: TMenuItem;
    mmDelBackup: TMenuItem;
    mmStandard: TMenuItem;
    N7: TMenuItem;
    mmOptimate: TMenuItem;
    N8: TMenuItem;
    pmSearch: TMenuItem;
    N6: TMenuItem;
    mmLang: TMenuItem;
    mmGer: TMenuItem;
    mmEng: TMenuItem;
    PageControl: TPageControl;
    tsStartup: TTabSheet;
    tsContext: TTabSheet;
    lwList: TListView;
    lStartup: TLabel;
    bActivate: TButton;
    bClose: TButton;
    bDeactivate: TButton;
    bDelete: TButton;
    bExport: TButton;
    lCopy1: TLabel;
    lCopy2: TLabel;
    bExportContext: TButton;
    bDeleteContext: TButton;
    bClose2: TButton;
    bDeactContext: TButton;
    bActContext: TButton;
    lwContext: TListView;
    lContext: TLabel;
    lVersion2: TLabel;
    lVersion: TLabel;
    pbLoad: TProgressBar;
    cbExpert: TCheckBox;
    lWindows2: TLabel;
    lWindows: TLabel;
    mmFra: TMenuItem;
    mmUpdate: TMenuItem;
    N9: TMenuItem;
    mmDownloadCert: TMenuItem;
    N10: TMenuItem;
    pmEdit: TMenuItem;
    mmReport: TMenuItem;
    mmRunOnce: TMenuItem;
    procedure FormCreate(Sender: TObject);
    procedure FormDestroy(Sender: TObject);
    procedure FormShow(Sender: TObject);
    procedure bDeleteClick(Sender: TObject);
    procedure bDeleteContextClick(Sender: TObject);
    procedure bActivateClick(Sender: TObject);
    procedure bActContextClick(Sender: TObject);
    procedure bDeactivateClick(Sender: TObject);
    procedure bDeactContextClick(Sender: TObject);
    procedure bExportClick(Sender: TObject);
    procedure bExportContextClick(Sender: TObject);
    procedure lwListSelectItem(Sender: TObject; Item: TListItem;
      Selected: Boolean);
    procedure lwContextSelectItem(Sender: TObject; Item: TListItem;
      Selected: Boolean);
    procedure lwContextDblClick(Sender: TObject);
    procedure lwListDblClick(Sender: TObject);
    procedure lwListColumnClick(Sender: TObject; Column: TListColumn);  //http://www.delphipraxis.net/283-wie-kann-ich-eine-listview-sortieren.html
    procedure lwListCompare(Sender: TObject; Item1, Item2: TListItem;   //http://www.delphipraxis.net/283-wie-kann-ich-eine-listview-sortieren.html
      Data: Integer; var Compare: Integer);
    procedure pmDeactivateClick(Sender: TObject);
    procedure pmInfosClick(Sender: TObject);
    procedure pmDeleteClick(Sender: TObject);
    procedure pmSearchClick(Sender: TObject);
    procedure pmEditClick(Sender: TObject);
    procedure mmImportClick(Sender: TObject);
    procedure mmRefreshClick(Sender: TObject);
    procedure mmContextClick(Sender: TObject);
    procedure mmExportListClick(Sender: TObject);
    procedure mmExportClick(Sender: TObject);
    procedure mmAddClick(Sender: TObject);
    procedure mmGerClick(Sender: TObject);
    procedure mmEngClick(Sender: TObject);
    procedure mmFraClick(Sender: TObject);
    procedure mmStandardClick(Sender: TObject);
    procedure mmOptimateClick(Sender: TObject);
    procedure mmDateClick(Sender: TObject);
    procedure mmDelBackupClick(Sender: TObject);
    procedure mmInfoClick(Sender: TObject);
    procedure mmUpdateClick(Sender: TObject);
    procedure mmDownloadCertClick(Sender: TObject);
    procedure mmReportClick(Sender: TObject);
    procedure PageControlChange(Sender: TObject);
    procedure cbExpertClick(Sender: TObject);
    procedure FormKeyDown(Sender: TObject; var Key: Word;
      Shift: TShiftState);
    procedure lCopy1MouseLeave(Sender: TObject);
    procedure lCopy1MouseEnter(Sender: TObject);
    procedure bCloseClick(Sender: TObject);
    procedure lCopy1Click(Sender: TObject);
  private
    FColumnToSort: Word;
    Startup: TStartupList;
    Context: TContextList;
    FTab: TTabSheet;
    FLang: TLanguageFile;
    FUpdateCheck: TUpdateCheck;
    procedure AfterUpdate(Sender: TObject; ADownloadedFileName: string);
    procedure BeforeUpdate(Sender: TObject; const ANewBuild: Cardinal);
    procedure ButtonHandle;
    procedure CountContext;
    procedure CountStartups;
    procedure EditPath(APath: string);
    procedure GetContext;
    procedure GetStartups(AGetNewData: Boolean = true);
    procedure OnSearchProgress(Sender: TObject; AWorkCount: integer);
    procedure OnSearchStart(Sender: TObject; AWorkCountMax: integer);
    procedure OnSearchEnd(Sender: TObject);
    procedure RegExport;
    procedure SetLanguage(Sender: TObject);
  public
    property Lang: TLanguageFile read FLang;
  end;

var
  Main: TMain;

implementation

{$R *.dfm}
{$R manifest.res}

{ TMain }

{ TMain.FormCreate

  VCL event that is called when form is being created. }

procedure TMain.FormCreate(Sender: TObject);
begin
  // German language default
  FLang := TLanguageFile.Create(100, Application);
  FLang.AddListener(Self);
  SetLanguage(Self);

  // Init update notificator
  FUpdateCheck := TUpdateCheck.Create(Self, 'Clearas', FLang);

  // Check for update on startup
  FUpdateCheck.CheckForUpdate(False);

  // Init Clearas instances
  FTab := tsStartup;
  Startup := TStartupList.Create;
  Context := TContextList.Create;

  // Set title
  Caption := Application.Title + TOSUtils.GetArchitecture();
end;

{ TMain.FormDestroy

  VCL event that is called when form is being destroyed. }

procedure TMain.FormDestroy(Sender: TObject);
begin
  Startup.Free;
  Context.Free;
end;

{ TMain.FormShow

  VCL event that is called when form is shown. }

procedure TMain.FormShow(Sender: TObject);
var
  windows: string;
  newWindows: Boolean;

begin
  windows := TOSUtils.GetWinVersion();
  newWindows := TOSUtils.CheckWindows();

  // Check for incompatibility
  if not (newWindows or (windows[1] in ['X','2'])) then
  begin
    Flang.MessageBox(FLang.GetString(74) + windows + FLang.GetString(75), mtError);
    mmExportList.Enabled := false;
    mmRefresh.Enabled := false;
    mmContext.Enabled := false;
    lwList.Enabled := false;
    lwContext.Enabled := false;
    cbExpert.Enabled := false;
    Exit;
  end;  //of if

  //"Deaktivierungsdatum" nur ab Vista
  mmDate.Enabled := newWindows;

  //Pfad in Kontextmenü aktualisieren
  mmContext.Checked := TClearas.UpdateContextPath(FLang);

  // Auslesen
  GetStartups;
end;

{ private TMain.AfterUpdate

  Event method that is called by TUpdate when download is finished. }

procedure TMain.AfterUpdate(Sender: TObject; ADownloadedFileName: string);
begin
  if (ExtractFileExt(ADownloadedFileName) <> '.reg') then
  begin
    // Caption "Search for update"
    mmUpdate.Caption := FLang.GetString(15);
    mmUpdate.Enabled := False;
  end  //of begin
  else
    mmDownloadCert.Enabled := False;
end;

{ private TMain.BeforeUpdate

  Event that is called by TUpdateCheck when TUpdateCheckThread finds an update. }

procedure TMain.BeforeUpdate(Sender: TObject; const ANewBuild: Cardinal);
begin
  // Show dialog: Ask for permitting download
  with FLang do
    if (MessageBox(Format(GetString(21) +^J+ GetString(22), [ANewBuild]),
      mtQuestion, True) = IDYES) then
      with TUpdate.Create(Self, FLang, FLang.GetString(24)) do
        Download('clearas.exe', 'Clearas.exe')
    else
      mmUpdate.Caption := FLang.GetString(24);
end;


procedure TMain.ButtonHandle;                          //Buttons de-/aktivieren
begin
  if (FTab = tsStartup) then                            //welche Page?
     begin
     if Startup.Item.Enabled then                       //Autostart
        begin
        bActivate.Enabled := false;
        bDeactivate.Enabled := true;
        bDeactivate.Default := true;
        end  //of begin
     else
        begin
        bDeactivate.Enabled := false;
        bActivate.Enabled := true;
        bActivate.Default := true;
        end;  //of if

     CountStartups;                                     //Zähler Refresh
     end  //of begin
  else
     begin                                              //Kontextmenü
     if Context.Item.Enabled then
        begin
        bActContext.Enabled := false;
        bDeactContext.Enabled := true;
        bDeactContext.Default := true;
        end  //of begin
     else
        begin
        bDeactContext.Enabled := false;
        bActContext.Enabled := true;
        bActContext.Default := true;
        end;  //of if

     CountContext;                                      //Zähler Refresh
     end;  //of begin
end;


procedure TMain.CountContext;
begin
  lwContext.Columns[1].Caption := FLang.GetString(87) +' ('+IntToStr(Context.ActCount)
                                   +'/'+ IntToStr(Context.Count)+')';     //Kontextmenü-Einträge zählen
end;


procedure TMain.CountStartups;
begin
  lwList.Columns[1].Caption := FLang.GetString(2)+'('+IntToStr(Startup.ActCount)
                               +'/'+IntToStr(Startup.Count)+')';        //aktive Programme zählen
end;


procedure TMain.EditPath(APath: string);
var
  FullPath: string;

  function AddCommas(APath: string): string;
  begin
    if (APath[1] <> '"') then
       result := '"'+ APath +'"'
    else
       result := APath;
  end;

begin
  with Startup do
    begin
    FullPath := InputBox(FLang.GetString(53), FLang.GetString(54), AddCommas(APath));  //Pfad bearbeiten Dialog

    if ((FullPath = '') or (FullPath = ' '))then
       FullPath := AddCommas(APath);                                   //Fehlervermeidung: Standard-Wert

    if Item.Enabled then                                               //Item aktiviert?
       TClearas.WriteStrValue(Item.RootKey, Item.KeyPath, Item.Name, FullPath)  //neuen Pfad in REG schreiben
    else
       begin                                                           //Item deaktiviert
       TClearas.WriteStrValue(Item.RootKey, Item.KeyPath, 'Command', FullPath); //neuen Pfad in REG schreiben
       TClearas.WriteStrValue(Item.RootKey, Item.KeyPath, 'Item', Item.Name);   //Name in REG schreiben
       end;  //of if

    lwList.ItemFocused.SubItems[1] := FullPath;                        //in Liste schreiben
    end;  //of begin
end;


procedure TMain.GetContext;                             //Kontextmenüs auslesen
var
  i: integer;

begin
  pbLoad.Visible := true;                            //Progressbar sichtbar machen
  pbLoad.Position := 0;                              //Progressbar auf 0
  Context.Clear;                                     //Kontextmenü Liste leeren

  try
    if cbExpert.Checked then
       begin
       Context.OnSearchBegin := OnSearchStart;       //Events verknüpfen
       Context.OnSearch := OnSearchProgress;
       Context.OnSearchEnd := OnSearchEnd;
       Application.ProcessMessages;                  //auslagern
       Context.AddEntry();                           //Einträge suchen
       end  //of begin
    else
       Context.Load;                                 //Kontextmenü-Einträge in Liste

    lwContext.Clear;                                 //Kontextmenü-ListView leeren

    for i := 0 to Context.Count -1 do                //Kontextmenüs ausgeben
      with lwContext.Items.Add do
        begin
        Caption := Context[i].GetStatus(FLang);
        SubItems.Append(Context[i].Name);
        SubItems.Append(Context[i].Location);
        SubItems.Append(Context[i].TypeOf);
        end; //of with

  finally
    CountContext;                                    //Zähler Refresh
    pbLoad.Visible := false;                         //Progressbar unsichtbar
  end;  //of finally
end;


procedure TMain.GetStartups(AGetNewData: Boolean = true);  //Autostart auslesen
var
  i: integer;

begin
  lwList.Clear;                                  //Autostart-ListView leeren

  try                                            
    if AGetNewData then                          //Daten neu auslesen?
       begin
       Startup.Clear;                            //init bzw Reset Zähler
       Startup.Load(mmRunOnce.Checked);          //Autostart auslesen
       end;  //of begin

    for i := 0 to Startup.Count -1 do            //Autostart-Programme ausgeben
      with lwList.Items.Add do
        begin
        Caption := Startup[i].GetStatus(FLang);
        SubItems.Append(Startup[i].Name);
        SubItems.Append(Startup[i].FilePath);
        SubItems.Append(Startup[i].TypeOf);

        if mmDate.Checked then
           SubItems.Append(Startup[i].Time);
        end;  //of with

    if bActivate.Enabled then
       bActivate.Enabled := false
    else
       bDeactivate.Enabled := false;

  finally                                        //freigeben
    CountStartups;                               //Zähler Refresh
  end;  //of finally
end;

{ Thread Events }
procedure TMain.OnSearchProgress(Sender: TObject; AWorkCount: integer);
begin
  pbLoad.Position := AWorkCount;                //ProgressBar Position
end;


procedure TMain.OnSearchStart(Sender: TObject; AWorkCountMax: integer);
begin
  pbLoad.Max := AWorkCountMax;                  //ProgressBar Ende
end;


procedure TMain.OnSearchEnd(Sender: TObject);
begin
  pbLoad.Visible := false;
  pbLoad.Position := 0;
end;


procedure TMain.RegExport;                                         //REG-Export
var
  SaveDialog: TSaveDialog;

begin
  SaveDialog := TSaveDialog.Create(Self);           //init SaveDialog

  with SaveDialog do
    begin
    Options := Options + [ofOverwritePrompt];       //Überschreiben-Dialog
    Filter := FLang.GetString(36);                  //Filter nur für *.reg festlegen
    DefaultExt := '.reg';                           //*.reg-Endung
    Title := FLang.GetString(33);                   //Fenstername

    if (FTab = tsStartup) then
       FileName := Startup.Item.Name + DefaultExt   //default-Name
    else
       FileName := Context.Item.Name +'_'+ Context.Item.Location + DefaultExt;  //default-Name
    end;  //of with

  try
    if SaveDialog.Execute then                      //"speichern" Klick
       if (FTab = tsStartup) then
          Startup.ExportItem(SaveDialog.FileName)   //"Speichern"
       else
          Context.ExportItem(SaveDialog.FileName);  //"Speichern"

  finally
    SaveDialog.Free;
  end;  //of finally
end;


procedure TMain.SetLanguage(Sender: TObject);           //Sprache lokalisieren
begin
  with FLang do
  begin
    tsContext.Caption := GetString(84);                   //Buttons Autostart
    bActivate.Caption := GetString(96);
    bDeactivate.Caption := GetString(97);
    bExport.Caption := GetString(98);
    bDelete.Caption := GetString(99);
    bClose.Caption := GetString(100);

    lStartup.Caption := GetString(90);                     //Liste Autostart
    lwList.Columns[0].Caption := GetString(91);
    lwList.Columns[2].Caption := GetString(101);
    lwList.Columns[3].Caption := GetString(94);

    bActContext.Caption := bActivate.Caption;             //Buttons Kontextmenü
    bDeactContext.Caption := bDeactivate.Caption;
    bExportContext.Caption := bExport.Caption;
    bDeleteContext.Caption := bDelete.Caption;
    bClose2.Caption := bClose.Caption;
    cbExpert.Caption := GetString(89);
    tsStartup.Caption := GetString(83);

    lContext.Caption := GetString(86);                    //Liste Kontextmenü
    lwContext.Columns[0].Caption := lwList.Columns[0].Caption;
    lwContext.Columns[1].Caption := GetString(87);
    lwContext.Columns[2].Caption := GetString(88);
    lwContext.Columns[3].Caption := GetString(94);

    mmFile.Caption := GetString(101);                      //"Datei"-Menü
    mmAdd.Caption := GetString(111);
    mmImport.Caption := GetString(112);
    mmExport.Caption := GetString(113);
    mmExportlist.Caption := GetString(114);
    mmClose.Caption := GetString(100);

    mmEdit.Caption := GetString(102);                      //"Bearbeiten"-Menü
    mmContext.Caption := GetString(104);
    mmDelBackup.Caption := GetString(105);

    mmView.Caption := GetString(20);                      //"Ansicht"-Menü
    mmRefresh.Caption := GetString(115);
    mmStandard.Caption := GetString(116);
    mmOptimate.Caption := GetString(117);
    mmDate.Caption := GetString(118);
    mmRunOnce.Caption := GetString(37);
    mmLang.Caption := GetString(119);

    mmHelp.Caption := GetString(14);                      //"Hilfe"-Menü
    mmUpdate.Caption := GetString(15);
    mmDownloadCert.Caption := GetString(16);
    mmReport.Caption := GetString(26);
    mmInfo.Caption := GetString(17);

    pmDeactivate.Caption := GetString(6);                 //Kontextmenü
    pmSearch.Caption := GetString(34);
    pmEdit.Caption := GetString(53);
    pmExport.Caption := GetString(8);
    pmDelete.Caption := GetString(9);
    pmInfos.Caption := GetString(35);
  end;  //of with
end;

{ Exportieren Events }
procedure TMain.bExportClick(Sender: TObject);              //Export als Backup
begin
  with Startup do
    if (Item.Enabled and Item.StartupUser) then
      begin
        if CreateBackup() then                                //Backup erstellen
          with Flang do
          begin
            MessageBox(GetString(44) +'"'+ TClearas.GetWinDir +'\pss\"'+ GetString(45));
            bExport.Enabled := false;
          end  //of with
        else
          Flang.MessageBox(46, mtError);
        end  //of begin
    else
      RegExport;                                              //Export als *.reg
end;


procedure TMain.bExportContextClick(Sender: TObject);     //Kontextmenü-Eintrag
begin                                                      //exportieren
  RegExport;
end;

{ Löschen Events }
procedure TMain.bDeleteClick(Sender: TObject);
var
  DelBackup, Exported: Boolean;

begin
  DelBackup := Startup.DeleteBackup;                     //Backup löschen speichern
  Exported := false;

  with FLang do
  begin
    if (MessageBox(GetString(48) +'"'+ Startup.Item.Name +'"'+ GetString(49) +^J
       + GetString(50) + GetString(57), mtConfirm) = IDYES) then  //wirklich löschen?
      begin
        //existiert Backup?
        if (bExport.Enabled and (MessageBox(GetString(52), mtQuestion) = IDYES)) then
        begin
          bExport.Click;
          DelBackup := false;
          Exported := true;
        end;  //of begin

        if ((not Exported) and Startup.Item.StartupUser and Startup.BackupExists) then
           Startup.DeleteBackup := (MessageBox(GetString(47), mtQuestion) = IDYES);  //Backup löschen?

        //Löschen + auf Fehler prüfen
        if Startup.DeleteItem() then
        begin
          lwList.DeleteSelected;                         //Element aus ListView löschen
          CountStartups;                                 //Zähler Refresh
        end  //of begin
        else
          FLang.MessageBox(FLang.GetString(65) + FLang.GetString(66), mtError);
      end;  //of if
  end;  //of with

  Startup.DeleteBackup := DelBackup;                     //Backup löschen zurücksetzen
end;


procedure TMain.bDeleteContextClick(Sender: TObject);     //Kontextmenü-Eintrag
begin                                                      //löschen
  with FLang do
  begin
    // wirklich löschen?
    if (MessageBox(GetString(85) +'"'+ Context.Item.Name +'"'+ GetString(49) +^J
       + GetString(50), mtConfirm) = IDYES) then
      begin
        // exportieren?
        if (MessageBox(GetString(52), mtQuestion) = IDYES) then
          RegExport;

       if Context.DeleteItem() then                       //löschen
          begin
          lwContext.DeleteSelected;                       //Element aus ListView löschen
          CountContext;                                   //Zähler Refresh
          end  //of begin
       else
          MessageBox(GetString(65) + GetString(66), mtError);
       end;  //of begin
    end;  //of with
end;

{ Aktivieren Events }
procedure TMain.bActivateClick(Sender: TObject);
begin
  if Startup.ChangeItemStatus() then                       //aktivieren + auf Fehler prüfen
  begin
    lwList.ItemFocused.Caption := Startup.Item.GetStatus(FLang); //optisch "Aktiviert" setzen
    ButtonHandle;                                         //"aktivieren"-Button deaktivieren
    bExport.Enabled := not Startup.BackupExists;

    if mmDate.Checked then                                //Deaktivierungsdatum verfügbar?
      lwList.ItemFocused.SubItems[3] := '';              //...dann Zeitpunkt aus Liste löschen
  end  //of begin
  else
    FLang.MessageBox(FLang.GetString(63) + FLang.GetString(66), mtError);
end;


procedure TMain.bActContextClick(Sender: TObject);        //Kontextmenü-Eintrag
begin                                                      //aktivieren
  if Context.ChangeItemStatus() then
  begin
    lwContext.ItemFocused.Caption := Context.Item.GetStatus(FLang);
    ButtonHandle;
  end  //of begin
  else
    FLang.MessageBox(FLang.GetString(63) + FLang.GetString(66), mtError);
end;

{ Deaktivieren Events }
procedure TMain.bDeactivateClick(Sender: TObject);
begin
  if Startup.ChangeItemStatus() then                        //Deaktivieren!
  begin
    lwList.ItemFocused.Caption := Startup.Item.GetStatus(FLang);  //optisch "Deaktiviert" setzen
    ButtonHandle;                                          //"deaktivieren"-Button deaktivieren

    if mmDate.Checked then                                 //Deaktivierungsdatum verfügbar
      lwList.ItemFocused.SubItems[3] := Startup.Item.Time;  //...dann in Liste schreiben

    if (Startup.Item.StartupUser and not bExport.Enabled) then
      bExport.Enabled := true;
  end  //of begin
  else
    FLang.MessageBox(FLang.GetString(64) + FLang.GetString(66), mtError);
end;


procedure TMain.bDeactContextClick(Sender: TObject);      //Kontextmenü-Eintrag
begin                                                      //deaktivieren
  if Context.ChangeItemStatus() then
  begin
    lwContext.ItemFocused.Caption := Context.Item.GetStatus(FLang);
    ButtonHandle;
  end  //of begin
  else
    FLang.MessageBox(FLang.GetString(64) + FLang.GetString(66), mtError);
end;

{ Selektion Events }
procedure TMain.lwListSelectItem(Sender: TObject; Item: TListItem;
  Selected: Boolean);
var
  Index: Word;

  function IsDouble(AName: string; AStartIndex: Integer): Integer;
  var
    i, j, k: Integer;

  begin
    result := -1;
    j := 0;

    for i := AStartIndex +1 to lwList.Items.Count -1 do
      if (AName = lwList.Items.Item[i].SubItems[0]) then
         begin
         Inc(j);
         result := i;
         Break;
         end;  //of begin

    if (j = 0) then
       for k := 0 to Item.Index -1 do
         if (AName = lwList.Items.Item[k].SubItems[0]) then
            begin
            result := k;
            Break;
            end;  //of begin
  end;

begin
  if (Selected and (Item <> nil)) then         //Item selektiert?
      begin
      Index := Startup.IndexOf(Item.SubItems[0], (Item.Caption = FLang.GetString(31)));
      Startup.Item := Startup.Items[Index];    //Pointer setzen
      bClose.Default := false;

      if (Startup.Item.StartupUser and Startup.Item.Enabled and Startup.BackupExists) then
         begin
         bExport.Enabled := false;
         pmExport.Enabled := false;
         end //of begin
      else
         begin
         bExport.Enabled := true;
         pmExport.Enabled := true;
         end;  //of if

      bDelete.Enabled := true;
      pmInfos.Enabled := true;
      pmDelete.Enabled := true;

      if ((Item.SubItems[2] <> '') and ((Item.SubItems[1] = '') or
         (Item.SubItems[1] = ' '))) then     //Schlüssel existiert und Pfad fehlt
         begin                               //...dann kann User danach suchen
         bActivate.Enabled := false;
         bDeactivate.Enabled := false;
         pmDeactivate.Enabled := false;
         bExport.Enabled := false;
         pmExport.Enabled := false;
         bDelete.Default := true;
         pmSearch.Visible := true;
         end  //of begin
      else
         begin
         if ((Item.Data = Startup.AppIndex) and (Assigned(Startup.AppIndex) or
            Assigned(Item.Data))) then     //ist Item das geänderte Programm?
            begin                         
            pmSearch.Visible := true;

            if pmEdit.Enabled then        //dann erlauben, den Pfad zu ändern
               pmEdit.Visible := true;
            end  //of begin
         else
            begin
            pmSearch.Visible := false;    
            pmEdit.Visible := false;
            end;  //of if

         if (Item.SubItems[2] = '') then  //Schlüssel fehlt
            begin
            bActivate.Enabled := false;
            bDeactivate.Enabled := false;
            bExport.Enabled := false;
            pmExport.Enabled := false;
            pmDeactivate.Enabled := false;
            bDelete.Default := true;
            end  //of begin
         else
            begin
            if (IsDouble(Item.SubItems[0], Item.Index) <> -1) then
               begin
               bActivate.Enabled := false;
               bDeactivate.Enabled := false;
               bDelete.Default := true;
               end  //of begin
            else
               begin
               if bDelete.Default then
                  bDelete.Default := false;

               if Startup.Item.Enabled then                       //aktiviert?
                  begin                                           //JA
                  bDelete.Default := false;
                  bActivate.Enabled := false;
                  bDeactivate.Enabled := true;
                  bDeactivate.Default := true;
                  pmDeactivate.Enabled := true;
                  pmDeactivate.Caption := FLang.GetString(7);  //Beschriftung ändern
                  end //of begin
               else
                  begin                                           //NEIN
                  bDelete.Default := false;
                  bActivate.Enabled := true;
                  bDeactivate.Enabled := false;
                  bActivate.Default := true;
                  pmDeactivate.Enabled := true;
                  pmDeactivate.Caption := FLang.GetString(6);  //Beschriftung ändern
                  end; //of if

               if not KeyPreview then                 //Hotkeys aktiviert?
                  KeyPreview := true;
               end; //of if
            end;  //of if
         end;  //of if
      end  //of begin
  else                                             //nichts angewählt, dann
     begin                                         //Buttons deaktivieren
     if bActivate.Enabled then
        begin
        bActivate.Default := false;
        bActivate.Enabled := false;
        end  //of begin
     else
        begin
        bDeactivate.Default := false;
        bDeactivate.Enabled := false;
        end;  //of if

     pmDeactivate.Enabled := false;
     pmDelete.Enabled := false;
     pmExport.Enabled := false;
     pmInfos.Enabled := false;
     bExport.Enabled := false;
     bDelete.Enabled := false;
     bClose.Default := true;
     end;  //of if
end;


procedure TMain.lwContextSelectItem(Sender: TObject; Item: TListItem;
  Selected: Boolean);
var
  Index: Word;

begin
  if (Selected and (Item <> nil)) then                  //Item selektiert?
      begin
      Index := Context.IndexOf(Item.SubItems[0], Item.SubItems[1]);
      Context.Item := Context.Items[Index];             //Pointer setzen
      bClose2.Default := false;
      bDeleteContext.Enabled := true;
      pmDelete.Enabled := true;
      bExportContext.Enabled := true;
      pmExport.Enabled := true;
      pmInfos.Enabled := true;
      pmInfos.Visible := (Item.SubItems[2] = 'Shell');  //Eigenschaften für Shell anzeigen

      if bDeletecontext.Default then
         bDeletecontext.Default := false;

      if (Item.Caption = FLang.GetString(31)) then    //aktiviert?
         begin
         bActContext.Enabled := false;                  //JA
         bDeactContext.Enabled := true;
         bDeactContext.Default := true;
         pmDeactivate.Enabled := true;
         pmDeactivate.Caption := FLang.GetString(7);  //Beschriftung ändern
         end //of begin
      else
         begin                                          //NEIN
         bActContext.Enabled := true;
         bDeactContext.Enabled := false;
         bActContext.Default := true;
         pmDeactivate.Enabled := true;
         pmDeactivate.Caption := FLang.GetString(6);  //Beschriftung ändern
         end; //of if
      end  //of begin
  else                                                  //nichts angewählt, dann
     begin                                              //Buttons deaktivieren
     if bActContext.Enabled then
        begin
        bActContext.Default := false;
        bActContext.Enabled := false;
        end  //of begin
     else
        begin
        bDeactcontext.Default := false;
        bDeactcontext.Enabled := false;
        end;  //of if

     pmDeactivate.Enabled := false;
     pmExport.Enabled := false;
     pmDelete.Enabled := false;
     pmInfos.Enabled := false;
     bExportContext.Enabled := false;
     bDeleteContext.Enabled := false;
     bClose2.Default := true;
     end;  //of if
end;

{ Doppelklick Events }
procedure TMain.lwListDblClick(Sender: TObject);
begin
  if bActivate.Enabled then
     bActivate.Click
  else
     if bDeactivate.Enabled then
        bDeactivate.Click
     else
        if bDelete.Enabled then
           bDelete.Click;
end;


procedure TMain.lwContextDblClick(Sender: TObject);
begin
  if bActContext.Default then
     bActContext.Click
  else
     if bDeactContext.Default then
        bDeactContext.Click
     else
        if bDeleteContext.Default then
           bDeleteContext.Click;
end;

{ Liste sortieren Event }
procedure TMain.lwListColumnClick(Sender: TObject; Column: TListColumn);
begin
  FColumnToSort := Column.Index;
  (Sender as TCustomListView).AlphaSort;
end;


procedure TMain.lwListCompare(Sender: TObject; Item1, Item2: TListItem;
  Data: Integer; var Compare: Integer);                     //ListView sortieren
begin
  if (FColumnToSort = 0) then                               //"Aktiv"-Spalte
     Compare := CompareText(Item1.Caption, Item2.Caption)
  else                                                      //andere Spalte
     begin
     Data := FColumnToSort -1;

     if (Data < 3) then
        Compare := CompareText(Item1.SubItems[Data], Item2.SubItems[Data]);
     end;  //of if
end;

{##############################################################################}
{ Kontextmenü }

procedure TMain.pmDeactivateClick(Sender: TObject);            //de-/aktivieren
begin
  if (FTab = tsStartup) then                    //Autostart
     lwListDblClick(Sender)
  else
     lwContextDblClick(Sender);                 //Kontext
end;


procedure TMain.pmDeleteClick(Sender: TObject);                       //löschen
begin
  if (FTab = tsStartup) then                    //Autostart
     bDelete.Click
  else
     bDeleteContext.Click;                      //Kontext
end;


procedure TMain.pmInfosClick(Sender: TObject);                  //Eigenschaften
var
  Path, Name: string;

begin
  if (FTab = tsStartup) then
    Startup.Item.GetItemInfo(Name, Path, FLang)
  else
    Context.Item.GetItemInfo(Name, Path, FLang);

  FLang.MessageBox(Name +^J+ Path);
end;


procedure TMain.pmSearchClick(Sender: TObject);          //nach Programm suchen
var
  OpenDialog: TOpenDialog;
  index: integer;

begin
  OpenDialog := TOpenDialog.Create(Self);                   //init OpenDialog

  with OpenDialog do
    begin
    InitialDir := TClearas.GetWinDir[1] +':\Program Files';  //Programme Verzeichnis
    Filter := FLang.GetString(38);                        //Filter
    Title := FLang.GetString(34);                         //Fenstername
    end;  //of with

  try
    if OpenDialog.Execute then                              //"öffnen" Klick
       begin
       index := lwList.ItemFocused.Index;                   //Index speichern
       lwList.ItemFocused.Data := Pointer(index);           //in Data schreiben
       Startup.AppIndex := lwList.ItemFocused.Data;         //Pointer auf Item
       EditPath(OpenDialog.FileName);                       //"Pfad bearbeiten"-Dialog
       lwListSelectItem(Self, lwList.ItemFocused, true);    //Eintrag selektieren
       pmEdit.Enabled := true;                              //"Pfad bearbeiten" in Kontextmenü
       end;  //of begin

  finally                                                   //freigeben
    OpenDialog.Free;
  end;  //of finally
end;


procedure TMain.pmEditClick(Sender: TObject);               //"Pfad bearbeiten"
begin
  EditPath(lwList.ItemFocused.SubItems[1]);
end;
{ of Kontextmenü }

{##############################################################################}
{ Mainmenu }

procedure TMain.mmAddClick(Sender: TObject);              //Programm hinzufügen
var
  OpenDialog: TOpenDialog;
  Name, Params: string;
  
  procedure CreateLnkFile(AErrorID: Integer = 55);       //*.Lnk Datei erstellen
  begin
	  if (Startup.IndexOf(Name) = -1) then                  //existiert Programm?
      if not TClearas.CreateLnk(OpenDialog.FileName, TClearas.GetStartUpDir(false) + Name) then
        FLang.MessageBox(AErrorID, mtError)
      else
        GetStartups
    else
      FLang.MessageBox(FLang.GetString(40) +'"'+ Name +'" '+ FLang.GetString(42));
  end;
  
begin
  OpenDialog := TOpenDialog.Create(Self);                   //init OpenDialog

  with OpenDialog do
    begin
    InitialDir := TClearas.GetWinDir[1] +':\Program Files';  //Programme Verzeichnis
    Filter := FLang.GetString(38);                        //Filter
    Title := FLang.GetString(15);                         //Fenstername
    end;  //of with

  try
    if OpenDialog.Execute then                              //"Öffnen" Klick
       with TClearas do
         case OpenDialog.FilterIndex of
           1: begin
              Name := DeleteExt(ExtractFileName(OpenDialog.FileName)) +'.lnk';
              CreateLnkFile(55);                            //*.Lnk Datei erstellen
              end;  //of begin

           2: begin
              Name := ExtractFileName(OpenDialog.FileName);
			        CreateLnkFile(56);                            //*.Lnk Datei erstellen
              end;  //of begin

           3: begin
              Name := DeleteExt(ExtractFileName(OpenDialog.FileName));  //Name für Eintrag vorschlagen
              Name := InputBox(FLang.GetString(106), FLang.GetString(107), Name);   //Name bearbeiten

              if (Name = '') then
                 Exit;
                 
              if not InputQuery(FLang.GetString(109), FLang.GetString(110), Params) then  //evtl. Parameter
                 Exit;

              if (Startup.IndexOf(Name) = -1) then                      //existiert Programm?
                 try
                   WriteStrValue('HKCU', KEY_STARTUP, Name, OpenDialog.FileName +' '+ Params);
                   GetStartups;

                 except
                   FLang.MessageBox(FLang.GetString(40) +'"'+ Name +'" '+ FLang.GetString(42), mtError);
                 end;  //of except
              end;  //of begin
         end;  //of case

  finally                                                   //freigeben
    OpenDialog.Free;
  end;  //of finally
end;


procedure TMain.mmImportClick(Sender: TObject);            //Backup importieren
var
  OpenDialog: TOpenDialog;
  Path, Name: string;
  
  procedure CreateLnkFile(AAllUsers: Boolean);      //*.Lnk Datei erstellen
  begin
    if (Startup.IndexOf(Name) = -1) then                  //existiert Programm?
    begin
      TClearas.CreateLnk(Path, TClearas.GetStartupDir(AAllUsers) + Name); //Currnt User Dir
      GetStartups;                                       //aktualisieren
    end  //of begin
    else
      FLang.MessageBox(FLang.GetString(40) +'"'+ Name +'" '+ FLang.GetString(42), mtError);
  end;
  
begin
  OpenDialog := TOpenDialog.Create(Self);                   //init OpenDialog

  if not DirectoryExists(TClearas.GetWinDir +'\pss') then   //existiert Dir nicht?
     ForceDirectories(TClearas.GetWinDir +'\pss');          //dann Dir erstellen

  with OpenDialog do
    begin
    InitialDir := TClearas.GetWinDir +'\pss';               //Zeiger auf Dir
    Filter := FLang.GetString(39) +' *'+ EXT_COMMON +'|*'+ EXT_COMMON;
    Title := FLang.GetString(16);
    end;  //of with

  try
    if OpenDialog.Execute then                              //"speichern" Klick
      with TClearas do
      begin
        ReadLnkFile(OpenDialog.FileName, Path);            //Verknüpfung auslesen
        Name := ExtractFileName(DeleteExt(OpenDialog.FileName));
        CreateLnkFile(OpenDialog.FilterIndex = 2);
      end;  //of with

  finally                             //freigeben
    OpenDialog.Free;
  end;   //of finally
end;


procedure TMain.mmExportClick(Sender: TObject);         //exportieren als *.reg
begin
  if ((FTab = tsStartup) and bExport.Enabled) then
    bExport.Click
  else
    if ((FTab = tsContext) and bExportContext.Enabled) then
      RegExport
    else
      FLang.MessageBox(FLang.GetString(58) + FLang.GetString(66) +^J
                  + FLang.GetString(59), mtWarning);
end;


procedure TMain.mmExportListClick(Sender: TObject);     //exportieren als Liste
var
  SaveDialog: TSaveDialog;

begin
  SaveDialog := TSaveDialog.Create(Self);           //init SaveDialog

  with SaveDialog do
  begin
    Options := Options + [ofOverwritePrompt];       //Überschreiben-Dialog
    Filter := FLang.GetString(36);                 //Filter festlegen
    DefaultExt := '.reg';                           //*.reg-Endung
    Title := FLang.GetString(18);                 //Fenstername
    FileName := FLang.GetString(11) + DefaultExt; //default-Name
  end;  //of with

  try
    if SaveDialog.Execute then                      //"speichern" Klick
      Startup.ExportList(SaveDialog.FileName);     //speichern als *.reg

  finally
    SaveDialog.Free;
  end;  //of finally
end;


procedure TMain.mmDelBackupClick(Sender: TObject);
begin
  Startup.DeleteBackup := not Startup.DeleteBackup;
end;


procedure TMain.mmContextClick(Sender: TObject);          //Kontextmenü-Eintrag
begin
  mmContext.Checked := TClearas.RegisterInContextMenu(mmContext.Checked, FLang);
end;


procedure TMain.mmRefreshClick(Sender: TObject);              //"aktualisieren"
begin
  if (FTab = tsStartup) then              //Reiter Autostart aktiv?
     GetStartups
  else
     GetContext;                          //Reiter Kontextmenü aktiv?
end;


procedure TMain.mmStandardClick(Sender: TObject);       //Standard Spaltengröße
begin
  if (FTab = tsStartup) then                             //Reiter Autostart
     begin
     lwList.Columns[1].Width := 125;
     lwList.Columns[2].Width := 124;
     lwList.Columns[3].Width := 80;
     end  //of begin
  else
     begin                                               //Reiter Kontextmenü
     lwContext.Columns[1].Width := 150;
     lwContext.Columns[2].Width := 115;
     lwContext.Columns[3].Width := 65;
     end;  //of if
end;


procedure TMain.mmOptimateClick(Sender: TObject);     //Spaltengröße optimieren
begin
  if (FTab = tsStartup) then                           //Reiter Autostart
     begin
     lwList.Columns[1].Width := ColumnTextWidth;
     lwList.Columns[2].Width := ColumnTextWidth;
     lwList.Columns[3].Width := ColumnTextWidth;
     end  //of begin
  else
     begin                                             //Reiter Kontextmenü
     lwContext.Columns[1].Width := ColumnTextWidth;
     lwContext.Columns[2].Width := ColumnTextWidth;
     end;  //of if
end;


procedure TMain.mmDateClick(Sender: TObject);                  //Datum anzeigen
var
  endOf: integer;

begin
  if not (WindowState = wsMaximized) then
     lwList.Clear;

  if mmDate.Checked then
     begin                            //Spalte löschen
     mmDate.Checked := false;

     if (lwList.Columns.Count > 4) then
        begin
        Constraints.MinWidth := Constraints.MinWidth - lwList.Column[4].Width;
        lwList.Column[4].MinWidth := 0;
        endOf := Width - 120;

        repeat                        //Animation "langsam zuschieben"
          Width := Width - 1;

          if (WindowState = wsMaximized) then
             endOf := Width;

          if (lwList.Column[4].Width <= 120) then
             lwList.Column[4].Width := lwList.Column[4].Width - 1;

          Update;
          Sleep(0);
        until ((lwList.Column[4].Width <= 0) and (Width = endOf));

        lwList.Columns.Delete(4);
        GetStartups(false);           //aktualisieren
        end;   //of if
     end   //of begin
  else
     begin                            //neue Spalte anlegen
     mmDate.Checked := true;
     lwList.Columns.Add.Caption := FLang.GetString(95);
     endOf := Width + 120;

     if (WindowState = wsMaximized) then
        GetStartups(false);           //aktualisieren

     repeat                           //Animation "langsam aufschieben"
       Width := Width + 1;

       if (WindowState = wsMaximized) then
          endOf := Width;

       if (lwList.Column[4].Width < 120) then
          lwList.Column[4].Width := lwList.Column[4].Width + 1;

       Update;
       Sleep(0);
     until ((lwList.Column[4].Width >= 120) and (Width = endOf));

     lwList.Column[4].MinWidth := lwList.Column[4].Width;
     lwList.Column[4].MaxWidth := lwList.Column[4].Width;
     Constraints.MinWidth := Constraints.MinWidth + lwList.Column[4].Width;

     if not (WindowState = wsMaximized) then
        GetStartups(false);           //aktualisieren
     end;  //of if
end;


{ TMain.mmGerClick

  MainMenu entry that allows to change the current language to german. }

procedure TMain.mmGerClick(Sender: TObject);
begin
  FLang.ChangeLanguage(Sender, 100);
end;

{ TMain.mmEngClick

  MainMenu entry that allows to change the current language to english. }

procedure TMain.mmEngClick(Sender: TObject);
begin
  FLang.ChangeLanguage(Sender, 300);
end;

{ TMain.mmFraClick

  MainMenu entry that allows to change the current language to french. }

procedure TMain.mmFraClick(Sender: TObject);
begin
  FLang.ChangeLanguage(Sender, 500);
end;


{ TMain.mmDownloadCertClick

  MainMenu entry that allows to download the PM Code Works certificate. }

procedure TMain.mmDownloadCertClick(Sender: TObject);
begin
  // Certificate already installed?
  if (TOSUtils.PMCertExists() and (FLang.MessageBox(FLang.GetString(27) +^J+
    FLang.GetString(28), mtQuestion) = IDYES)) then
    // Download certificate
    with TUpdate.Create(Self, FLang, FLang.GetString(16)) do
      DownloadCertificate();
end;

{ TMain.mmUpdateClick

  MainMenu entry that allows users to manually search for updates. }

procedure TMain.mmUpdateClick(Sender: TObject);
begin
  FUpdateCheck.CheckForUpdate(True);
end;

{ TMain.mmReportClick

  MainMenu entry that allows users to easily report a bug by opening the web
  browser and using the "report bug" formular. }

procedure TMain.mmReportClick(Sender: TObject);
begin
  TOSUtils.OpenUrl(URL_CONTACT);
end;

{ TMain.mmInfoClick

  MainMenu entry that shows a info page with build number and version history. }

procedure TMain.mmInfoClick(Sender: TObject);
var
  Info: TInfo;

begin
  Application.CreateForm(TInfo, Info);
  Info.ShowModal;
  Info.Free;
end;

{ TMain.lCopyClick

  Opens the homepage of PM Code Works in a web browser. }

procedure TMain.lCopy1Click(Sender: TObject);
begin
  TOSUtils.OpenUrl(URL_BASE);
end;

{ TMain.lCopyMouseEnter

  Allows a label to have the look like a hyperlink. }

procedure TMain.lCopy1MouseEnter(Sender: TObject);
begin
  with (Sender as TLabel) do
  begin
    Font.Style := Font.Style + [fsUnderline];
    Font.Color := clBlue;
    Cursor := crHandPoint;
  end;  //of with
end;

{ TMain.lCopyMouseLeave

  Allows a label to have the look of a normal label again. }

procedure TMain.lCopy1MouseLeave(Sender: TObject);
begin
  with (Sender as TLabel) do
  begin
    Font.Style := Font.Style - [fsUnderline];
    Font.Color := clBlack;
    Cursor := crDefault;
  end;  //of with
end;


procedure TMain.PageControlChange(Sender: TObject);            //Modus wechseln
begin
  FTab := PageControl.ActivePage;
  mmExport.Enabled := true;
  mmExport.Visible := true;

  if (FTab = tsContext) then                       //Reiter Kontextmenü aktiv?
     begin
     mmAdd.Visible := false;                       //Einträge unsichtbar
     mmImport.Visible := false;
     mmExportList.Visible := false;
     mmDate.Visible := false;
     mmRunOnce.Visible := false;
     N2.Visible := false;
     pmInfos.Visible := false;

     if (Context.Count = 0) then
        GetContext;
     end  //of begin
  else
     begin                                         //Reiter Autostart
     mmAdd.Visible := true;                        //Einträge sichtbar
     mmImport.Visible := true;
     mmExportList.Visible := true;
     mmDate.Visible := true;
     mmRunOnce.Visible := true;
     N2.Visible := true;
     pmInfos.Visible := true;
     end;  //of if
end;


procedure TMain.cbExpertClick(Sender: TObject);
begin
  GetContext;
end;


procedure TMain.FormKeyDown(Sender: TObject; var Key: Word;
  Shift: TShiftState);                                                  //Hotkey
begin
  if (Key = VK_DELETE) then                   //"löschen"-Hotkey
     if ((FTab = tsStartup) and bDelete.Enabled) then
        bDelete.Click
     else
        if ((FTab = tsContext) and bDeleteContext.Enabled) then
           bDeleteContext.Click;
end;


procedure TMain.bCloseClick(Sender: TObject);
begin
  Close;
end;

end.